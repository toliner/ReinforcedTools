buildscript {
    ext.kotlin_version = '1.3.31'
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'forge'

version = "3.0.0"
group= "net.toliner.reinforcedtools"
archivesBaseName = "ReinforcedTools"

tasks.withType(Jar) {
    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileKotlin{
    kotlinOptions{
        jvmTarget = '1.8'
        includeRuntime = true
    }
}

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "run"
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    embed "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }

    manifest {
        attributes "FMLCorePlugin": "net.toliner.reinforcedtools.CorePlugin"
        attributes "FMLCorePluginContainsFMLMod": "true"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}